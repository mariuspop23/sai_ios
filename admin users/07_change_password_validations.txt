*** Settings ***
Documentation     A test suite containing tests for validating the change password form.

Suite Setup       Open browser and login user

Test Setup        Open Admin dropdown and select Change password
Test Template     Change password With Invalid Credentials Should Fail

Resource          ../resource_ios.txt
Resource          ../variables_ios.txt


*** Test Cases ***                  OLD PASSWORD             NEW PASSWORD              CONFIRM PASSWORD        ERROR MESSAGE
Empty fields                        ${EMPTY}                 ${EMPTY}                  ${EMPTY}                The password you entered is incorrect
Invalid old password                invalid                  ${VALID PASSWORD}         ${VALID PASSWORD}       The password you entered is incorrect
Limit chars under 8                 ${VALID PASSWORD}        Test123                   Test123                 Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Valid Uppercase                ${VALID PASSWORD}        VALIDUPPER                VALIDUPPER              Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Valid Lowercase                ${VALID PASSWORD}        VALIDLOWER                VALIDLOWER              Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Valid Alphanumerics            ${VALID PASSWORD}        123456789                 123456789               Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Uppercase and Lowercase        ${VALID PASSWORD}        TESTlowercase             TESTlowercase           Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Uppercase and Alphanumeric     ${VALID PASSWORD}        TEST123456                TEST123456              Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Only Lowercase and Alphanumeric     ${VALID PASSWORD}        test123456                test123456              Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters
Not maching new passwords           ${VALID PASSWORD}        Test123456                Test654321              Password must be 8 characters longer and must contain Uppercase, Lowercase and Alphanumeric characters


*** Keywords ***

Change password With Invalid Credentials Should Fail
    [Arguments]             ${old_password}         ${new_password}         ${confirm_password}         ${error_message}
    input text              old_passwd              ${old_password}
    input text              passwd                  ${new_password}
    input text              confirm_passwd          ${confirm_password}
    click button            submitChangePassword
    wait until element is enabled                   css=div.alert.alert-danger
    page should contain element                     css=div.alert.alert-danger          ${error_message}